10  '|------------------------------------------------------------------------------------------------------------------|
20  '|  DECLARACION DE VARIABLES - Entradas y Salidas                                                                                       |
30  '|------------------------------------------------------------------------------------------------------------------|

40  'Entradas
50  DEF IO ASTART   = BIT,1         'Start / Botón de Start en el Panel de Control
60  DEF IO ASTOP    = BIT,2         'Stop / Botón de Stop en el Panel de Control
70  DEF IO RRESET   = BIT,4         'Richten / Botón de Reset en el Panel de Control

80  'Salidas
90  DEF IO HSTART   = BIT,0         'Luz Piloto de Start en el Panel de Control
100  DEF IO HRESET   = BIT,1        'Luz Piloto de Reset en el Panel de Control
110  DEF IO LEDQ1    = BIT,2        'Luz Piloto Q1
120  DEF IO LEDQ2    = BIT,3        'Luz Piloto Q2


130  '|------------------------------------------------------------------------------------------------------------------|
140  '|  DECLARACION DE VARIABLES DE ESTADO DEL CNC, LUCES PILOTO DE START Y RESET.                                                                                          |
150  '|------------------------------------------------------------------------------------------------------------------|


160 M_CNCNR = 0                                           'Variable de Disponibilidad del CNC
170 HSTART = 0                                            'Luz Piloto de Start inicia en 0
180 HRESET = 0                                            'Luz Piloto de Reset inicia en 0
190 M_OUTW(32) = 0                                        'Reset outputword 32 (Out32-Out39)
200 M_OUTW(40) = 0                                        'Reset outputword 40 (Out40-Out47)


210  '|------------------------------------------------------------------------------------------------------------------|
220  '|  LA SECCIÓN LOOP2 INICIA UNA SECUENCIA DE PARPADEO EN LA LUZ DE RESET. PARA PONER EL SISTEMA EN DISPONIBILIDAD   |
230  '|  SE DEBE DE PRESIONAR EL BOTON DE RESET. EL PARPADEO CONTINUARA HASTA QUE SE PRESIONE EL BOTON                   |
240  '|------------------------------------------------------------------------------------------------------------------|


250 *LOOP2
260 HRESET = 1                                            'Luz Piloto de Reset se enciende en el panel de control
270 DLY 0.5                                               'Delay 0.5 s
280 HRESET = 0                                            'Luz Piloto de Reset se apaga en el panel de control
290 DLY 0.5                                               'Delay 0.5 s
300 IF RRESET = 0 THEN GOTO *LOOP2                        'Si el boton de Reset no ha sido presionado, el programa continua en el Loop 2.
310 HRESET = 0                                            'Si se presiona el boton de Reset: se apaga la luz piloto de reset.
320 CALLP"CNCDRV40","RESET" 								   'Corre el programa del Torno y el programa Reset
330 CALLP"CNCDRV32","RESET" 						     	   'Corre el programa de la Fresa y el programa Reset
340 MOV PINIT												   'Se mueve el brazo a la posición PINIT


350  '|------------------------------------------------------------------------------------------------------------------|
360  '|  LA SECCIÓN LOOP3 INICIA UNA SECUENCIA DE PARPADEO EN LA LUZ DE START. ESTO PARA INDICAR QUE EL SISTEMA ESTA EN  |
370  '|  EN DISPONIBILIDAD. PARA INICIAR EL PROGRAMA SE DEBE DE PRESIONAR EL BOTON DE START.                             |
380  '|------------------------------------------------------------------------------------------------------------------|



390 *LOOP3
400 HSTART = 1                                            'Luz Piloto de Start se enciende en el panel de control
410 DLY 0.5                                               'Delay 0.5 s
420 HSTART = 0                                            'Luz Piloto de Start se apaga en el panel de control
430 DLY 0.5                                               'Delay 0.5 s
440 IF ASTART = 0 THEN GOTO *LOOP3                        'Si el boton de Start no ha sido presionado, el programa continua en el Loop 3.
450 HSTART = 0                                            'Luz Piloto de Start en el Panel de Control se apaga



460  '|------------------------------------------------------------------------------------------------------------------|
470  '|  El siguiene sección controla la operación de la banda transportadora 1.
480  '|  
490  '|  Los bits de MEmoria M_001 y M_002 indican la posición inicial y final del objeto respectivamente.
500  '|  La secuencia inicia colocando el bit de inicio en la Banda Transportadora 1 y lo mueve a la 4 (Torno).
510  '|  Una vez finaliza, lo lleva a la fresadora (5). Finalizada la secuencia de la fresa, mueve la pieza en la banda
520  '|  transportadora 1. Luego, lo mueve de nuevo a la fresadora pero usando el programa MOVE2, el cual tiene cambiado
530  '|  el parametro de la posición del efector final, el cual esta rotado 180° para que la pieza pueda ser maquinada en
540  '|  el otro extremo. Una vez finalizado el segundo proceso, la pieza es llevada a la banda transportadora 3 (salida).
550  '|  
560  '|  Cabe mencionar que esto cumple con el objetivo de maquinar la pieza por segunda vez si la pieza habia sido
570  '|  anteriormente torneada y fresada, en ese orden.
580  '|------------------------------------------------------------------------------------------------------------------|


590 M_00 = 1										'INDICA LA POSICION DE ORIGEN DE LA PIEZA. 1 = BANDA TRANSPORTADORA 1
600 M_01 = 4										'INDICA EL DESTINO. 4 = TORNO
610 CALLP "MOVE"									'EJECUTA EL PROGRAMA MOVE
620 CALLP"CNCDRV40","RUNPR" 						'EJECUTA EL PROGRAMA CNDRV40 EL CUAL CORRESPONDE AL TORNO
630 M_00 = 4										'INDICA EL ORIGEN. 4 = TORNO
640 M_01 = 5										'INDICA EL DESTINO. 5 = FRESADORA
650 CALLP "MOVE"									'EJECUTA EL PROGRAMA MOVE
660 CALLP"CNCDRV32","RUNPR" 						'EJECUTA EL PROGRAMA CNDRV32 EL CUAL CORRESPONDE A LA FRESADORA
670 M_00 = 5										'INDICA EL ORIGEN. 5 = FRESADORA
680 M_01 = 1										'INDICA EL DESTINO DE LA PIEZA. 1 = BANDA TRANSPORTADORA 1
690 CALLP "MOVE"									'EJECUTA EL PROGRAMA MOVE
700 M_00 = 1										'INDICA LA POSICION DE ORIGEN DE LA PIEZA. 1 = BANDA TRANSPORTADORA 
710 M_01 = 5										'INDICA EL DESTINO. 5 = FRESADORA. 2DA VUELTA
720 CALLP "MOVE2"									'EJECUTA MOVE2 EL CUAL MUEVE LA PIEZA A LA FRESADORA CON LA POSICION FINAL INVERTIDA 180°
730 CALLP"CNCDRV32","RUNPR" 						'EJECUTA EL PROGRAMA CNDRV32 EL CUAL CORRESPONDE A LA FRESADORA
740 M_00 = 5										'INDICA EL ORIGEN. 5 = FRESADORA
750 M_01 = 3										'INDICA EL DESTINO. 3 = BANDA TRANSPORADORA 3
760 CALLP "MOVE"									'EJECUTA EL PROGRAMA MOVE



770  '|------------------------------------------------------------------------------------------------------------------|
780  '|  LA SIGUIENTE SECCION CONTROLA LA OPERACION DE LA BANDA TRANSPORTADORA 2.
790  '|  
800  '|  LOS BITS DE MEMORIA M_00 Y M_01 INDICAN LA POSICION INICIAL Y FINAL DEL OBJETO RESPECTIVAMENTE.
810  '|  LA SECUENCIA INICIA COLOCANDO EL BIT DE INICIO EN 2 (BANDA TRANSPORTADORA 2) Y LO MUEVE A LA 5 (FRESADORA)
820  '|  CON ESTOS BITS (VARIABLES GLOBALES) SE EJECUTA EL PROGRAMA MOVE EL CUAL SE ENCARGA DE MOVER LAS PIEZAS ENTRE
830  '|  POSICIONES SEGUN LOS DATOS EN LAS VARIABLES GLOBALES.
840  '|  UNA VEZ FINALIZADA LA SECUENCIA DE LA FRESADORA, LA PIEZA ES LLEVADA DE LA POSICIÓN 5(FRESADORA) HACIA LA 4 (TORNO)
850  '|  CUANDO FINALIZA EL PROGRAMA DEL TORNO, LA PIEZA ES LLEVADA DE LA POSICIÓN 4(TORNO) HACIA LA 3 (BANDA TRANSPORTADORA 3)
860  '|------------------------------------------------------------------------------------------------------------------|



870 M_00 = 2										'INDICA LA POSICION DE ORIGEN DE LA PIEZA. 2 = BANDA TRANSPORTADORA 2
880 M_01 = 5										'INDICA EL DESTINO. 5 = FRESADORA
890 CALLP "MOVE"									'EJECUTA EL PROGRAMA MOVE
900 CALLP"CNCDRV32","RUNPR" 						'EJECUTA EL PROGRAMA CNDRV32 EL CUAL CORRESPONDE A LA FRESADORA
910 M_00 = 5										'INDICA LA POSICION DE ORIGEN DE LA PIEZA. 5 = FRESADORA
920 M_01 = 4										'INDICA EL DESTINO. 4 = TORNO
930 CALLP "MOVE"									'EJECUTA EL PROGRAMA MOVE
940 CALLP"CNCDRV40","RUNPR" 						'EJECUTA EL PROGRAMA CNDRV40 EL CUAL CORRESPONDE AL TORNO
950 M_00 = 4										'INDICA LA POSICION DE ORIGEN DE LA PIEZA. 4 = TORNO
960 M_01 = 3										'INDICA EL DESTINO. 3 = BANDA TRANSPORADORA 3
970 CALLP "MOVE"									'EJECUTA EL PROGRAMA MOVE

980 CALLP "CDTURN"									'EL PROGRAMA CDTURN CIERRA LA PUERTA DEL TORNO
990 CALLP "CDMILL"									'EL PROGRAMA CDMILL CIERRA LA PUERTA DE LA FRESADORA

1000 END												'FINALIZA EL PROGRAMA
